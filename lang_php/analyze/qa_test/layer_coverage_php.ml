(* Yoann Padioleau
 *
 * Copyright (C) 2010 Facebook
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public License
 * version 2.1 as published by the Free Software Foundation, with the
 * special exception on linking described in file license.txt.
 * 
 * This library is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the file
 * license.txt for more details.
 *)

open Common

open Ast_php

(*****************************************************************************)
(* Prelude *)
(*****************************************************************************)
(*
 * Thin layer generator wrapper around the Coverage_tests_php.lines_coverage
 * data, which itself is generated by running many unit tests with a 
 * tracer, xdebug or hphpi-tracer, on.
 * 
 * We generate either a layer with red/green colors or one with
 * heat-based colors for finer-grained visualization.
 *)

(*****************************************************************************)
(* Types *)
(*****************************************************************************)

let red_green_properties = [
  "ok", "green";
  "bad", "red";
  "no_info", "white";
]

let heat_map_properties = [
  "cover 100%", "red3";
  "cover 90%", "red1";
  "cover 80%", "orange";
  "cover 70%", "yellow";
  "cover 60%", "YellowGreen";
  "cover 50%", "green";
  "cover 40%", "aquamarine3";
  "cover 30%", "cyan";
  "cover 20%", "DeepSkyBlue1";
  "cover 10%", "blue";
  (* we use "white" for 0 and not dark-blue (as it is the case usually with
   * heatmaps) because ... the picture would be too blue otherwise.
   *)
  "cover 0%", "white";

  (* when we zoom on a file we just show red/green coverage, no heat color *)
  "ok", "green";
  "bad", "red";
]

(*****************************************************************************)
(* Helper *)
(*****************************************************************************)

(*****************************************************************************)
(* Main entry point *)
(*****************************************************************************)

let gen_red_green_layer lines_coverage ~output =
  let layer = { Layer_code.
    title = "Test coverage (red/green)";
    description = "Use information from xdebug";
    files = lines_coverage +> List.map (fun (file, lines_cover) ->
      let covered = lines_cover.Coverage_tests_php.covered_call_sites in
      let all = lines_cover.Coverage_tests_php.call_sites in
      let not_covered = Common.minus_set all covered in
      let percent = 
        try 
          Common.pourcent_good_bad 
            (List.length covered) (List.length not_covered) 
       with Division_by_zero -> 0
      in
      file,
      { Layer_code.
        micro_level = 
          (covered +> List.map (fun line -> line, "ok")) ++
          (not_covered +> List.map (fun line -> line, "bad"))
        ;
        macro_level = [
          (match percent with
          | 0 -> "no_info"
          | n when n < 50 -> "bad"
          | _ -> "ok"
          ),
          1.
        ];
      }
    );
    kinds = red_green_properties;
  }
  in
  Layer_code.save_layer layer output


(* mostly a copy paste of gen_red_green_layer *)
let gen_heatmap_layer lines_coverage ~output =

  let layer = { Layer_code.
    title = "Test coverage (heatmap)";
    description = "Use information from xdebug";
    files = lines_coverage +> List.map (fun (file, lines_cover) ->
      let covered = lines_cover.Coverage_tests_php.covered_call_sites in
      let all = lines_cover.Coverage_tests_php.call_sites in
      let not_covered = Common.minus_set all covered in
      let percent = 
        try 
          Common.pourcent_good_bad 
            (List.length covered) (List.length not_covered) 
       with Division_by_zero -> 0
      in
      
      file,
      { Layer_code.
        micro_level = 
          (covered +> List.map (fun line -> line, "ok")) ++
          (not_covered +> List.map (fun line -> line, "bad"))
        ;
        macro_level = [
          (let percent_round = (percent / 10) * 10 in
          spf "cover %d%%" percent_round
          ),
          1.
        ];
      }
    );
    kinds = heat_map_properties;
  }
  in
  Layer_code.save_layer layer output


(*****************************************************************************)
(* Actions *)
(*****************************************************************************)

let actions () = [
  "-gen_red_green_coverage_layer", " <json> <output>",
  Common.mk_action_2_arg (fun jsonfile output ->
    let cover = Coverage_tests_php.load_lines_coverage jsonfile in
    gen_red_green_layer cover ~output
  );
  "-gen_heatmap_coverage_layer", " <json> <output>",
  Common.mk_action_2_arg (fun jsonfile output ->
    let cover = Coverage_tests_php.load_lines_coverage jsonfile in
    gen_heatmap_layer cover ~output
  );
]
